include conf/layerinfo.inc

INHERIT_DISTRO="devshell sstate license" 
INHERIT += "mvl"

INHERIT += "image-groups"

TCLIBCAPPEND=""
PACKAGE_CLASSES = "package_rpm"
IMAGE_FSTYPES_append = " ext2.gz"
#IMAGE Defaults

VIRTUAL-RUNTIME_login_manager ?= "busybox"
VIRTUAL-RUNTIME_initscripts ?= "initscripts"
VIRTUAL-RUNTIME_syslog ?= "syslog-ng"

# Set Vanity values.
TARGET_VENDOR="-montavista"
SDK_VENDOR = "-montavistasdk"
SDK_NAME_PREFIX = "montavista"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_OLDEST_KERNEL = "2.6.32"

# Emit shell script variables.
require conf/emit_shellscript.inc

# Export drlog.
require conf/drlog.inc

# Exportvars for devrocket.

INHERIT += "exportvars"
EXPORTVARSRUN="1"

# Generate mirror tarballs.

BB_GENERATE_MIRROR_TARBALLS ?= "1"

# Use Hashed stamps. 

BB_SIGNATURE_HANDLER = "OEBasicHash"

MVL_MIRROR="http://someplace.org/"
PREFERRED_PROVIDER_virtual/gettext ?= "gettext"
PREFERRED_PROVIDER_jpeg ?= "jpeg"
PREFERRED_PROVIDER_jpeg-native ?= "jpeg-native"
PREFERRED_PROVIDER_start-stop-daemon ?= "dpkg"
PREFERRED_PROVIDER_openssl ?= "openssl10"
PREFERRED_PROVIDER_openssl-native ?= "openssl10-native"

USE_SYSTEMD ?= "0"

DISTROOVERRIDES .= ":selinux"
IMAGE_FEATURES += "mvl package-management"
DISTRO_FEATURES_append =  "${@bb.utils.contains("BBFILE_COLLECTIONS", "selinux", " selinux ", "", d)}"
DISTRO_FEATURES_append =  "${@bb.utils.contains("BBFILE_COLLECTIONS", "virtualization-layer", " virtualization ", "", d)}"
DISTRO_FEATURES_append = " containers pam multiarch mvl opengl x11 ${@bb.utils.contains('USE_SYSTEMD', '1', 'systemd', 'sysvinit', d)} "
DISTRO_FEATURES_append = " ptest virtualization"

DISTRO_FEATURES_remove = "gobject-introspection-data"
DISTRO_FEATURES_BACKFILL_CONSIDERED = " ${@bb.utils.contains('USE_SYSTEMD', '1', 'sysvinit', '', d)} "
VIRTUAL-RUNTIME_init_manager = " ${@bb.utils.contains('USE_SYSTEMD', '1', 'systemd', 'sysvinit', d)} "
# Use extrausers to add extra user and group configurations.
INHERIT += "extrausers"

# Use blacklist
INHERIT += "pn-blacklist"
include conf/distro/include/mvl-blacklist.inc

include conf/distro/include/common-image.inc

# Don't die on dangling depends.
BB_DANGLINGAPPENDS_WARNONLY = "1"

#reduce to single lsb string.
NATIVELSBSTRING_mvista-cgx='universal'

# Do not error out if there is a kernel version mismatch.
KERNEL_VERSION_SANITY_SKIP="1"

#Turn off autoref for linux linaro this breaks parsing on no network.
AUTOREV_pn-linux-linaro = "8abe04ab20f5e2b7bc76b5e69860facc3a743760"
MERGEPERFILEDEPS_mvista-cgx = "0"

# Don't rename sources in a mirror
BB_NO_MODIFY_MIRROR = "1"
SUB_DISTRO ??= "Carrier Grade Express"
DISTRO_NAME="MontaVista ${SUB_DISTRO} Linux"
DISTRO_VERSION = "2.4.0"
DISTRO_CODENAME = "CGX2.4"

BB_HASHBASE_WHITELIST_append = " MVLFETCH_OPTS MVLBASE STAGING_KERNEL_DIR LM_LICENSE_FILE ENABLE_CGE7_LM MVL_TOOLS_TOPLEVEL_PREFIX MSD_VERSION "
BB_HASHBASE_WHITELIST_append = " PLUGIN_ID FEATURE_ID PLUGIN_NAME FEATURE_NAME VENDOR_NAME ADK_VERSION MSD_REVISION SSTATE_PKGARCH "
BB_HASHBASE_WHITELIST_append = " HOST_FLAGS_FIXUPS SITEINFO_BITS TOPDIR KERNEL_CFG_LOCATION CSL_VER_NATIVE_GCC HOST_USER_UID"
BB_HASHBASE_WHITELIST_append = " DRLOG_HANDLER"
BUILDSDK_LDFLAGS += "-Wl,-rpath,/a/silly/attempt/to/create/a/very/long/rpath/to/reserve/a/large/rpath/buffer/in/sdk/binary/files/"

export LM_LICENSE_FILE 
export ENABLE_CGE7_LM
SDKPATH = "/opt/${SDK_NAME_PREFIX}"

LICENSE_CREATE_PACKAGE ?= "1"

HOST_FLAGS_FIXUPS='${@bb.utils.contains("BUILD_ARCH", "i686", " -march=i686 ", "", d)}'
BUILD_CFLAGS_append = '${HOST_FLAGS_FIXUPS}'

PREFERRED_PROVIDER_virtual/kernel = "linux-mvista"
PREFERRED_PROVIDER_virtual/libintl ?= "glibc"
PREFERRED_VERSION_python-requests = "2.18.4"
#Perl is fixed locally waiting on upstream fix
PNBLACKLIST[libnet-dns-perl]=""

#Add cgx-profile support
INHERIT += "cgx-profiles"

BUILDCFG_VARS_append = " CGX_PROFILES "

# export complete path of compiler
OECMAKE_C_COMPILER_class-target = "$(which $(echo ${CC} | sed 's/^\([^ ]*\)./\1/'))"
OECMAKE_CXX_COMPILER_class-target = "$(which $(echo ${CXX} | sed 's/^\([^ ]*\)./\1/'))"

# Include feature selection file, which includes kernel fragments
# files (.cfg) and dependent userspace applications based on available
# profiles
include conf/distro/extra-features/select-features.inc

